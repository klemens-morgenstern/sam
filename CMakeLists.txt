# Generated by `boostdep --cmake sam`
# Copyright 2020 Peter Dimov
# Distributed under the Boost Software License, Version 1.0.
# https://www.boost.org/LICENSE_1_0.txt

cmake_minimum_required(VERSION 3.5...3.16)

project(boost_sam VERSION "${BOOST_SUPERPROJECT_VERSION}" LANGUAGES CXX)

add_library(boost_sam STATIC src/src.cpp)
add_library(Boost::sam ALIAS boost_sam)

add_link_options(--coverage)
add_compile_options(--coverage)

set(BOOST_SAM_IS_ROOT OFF)

if(CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
  set(BOOST_SAM_IS_ROOT ON)
endif()

if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/../../boost.css AND NOT BOOST_SAM_IS_ROOT)
  set(BOOST_SAM_SHOULD_BE_INLINE ON)
else()
  set(BOOST_SAM_SHOULD_BE_INLINE OFF)
endif()

option(BOOST_SAM_BUILD_INLINE "Configure sam as if part of the boost source tree" ${BOOST_SAM_SHOULD_BE_INLINE})

if(BOOST_SAM_BUILD_INLINE)
  #
  # Building inside Boost tree, but as a separate project e.g. on Travis or
  # other CI, or when producing Visual Studio Solution and Projects.
  if (BOOST_SAM_IS_ROOT)
    set(BOOST_INCLUDE_LIBRARIES sam)
    set(BOOST_EXCLUDE_LIBRARIES sam)
    set(CMAKE_FOLDER _deps)
    add_subdirectory(../.. _deps/boost EXCLUDE_FROM_ALL)
    unset(CMAKE_FOLDER)
  endif()
else()
  find_package(Boost REQUIRED system)
endif()

if (BOOST_SAM_STANDALONE)
  add_compile_definitions(BOOST_SAM_STANDALONE=1)
else()
  if (BOOST_SAM_BUILD_INLINE)
    target_link_libraries(boost_sam PUBLIC
            Boost::asio
            Boost::core
            Boost::system
            )
  else()
    target_link_libraries(boost_sam PUBLIC Boost::system)
  endif()

endif()

target_include_directories(boost_sam PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/include)

if(BUILD_TESTING AND EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/test/CMakeLists.txt")
  enable_testing()
  add_subdirectory(test)
endif()

if(BUILD_EXAMPLES AND NOT BOOST_SAM_STANDALONE AND EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/example/CMakeLists.txt")
  add_subdirectory(example)
endif()

if(BUILD_BENCH AND EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/bench/CMakeLists.txt")
  add_subdirectory(bench)
endif()